apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    mavenCentral()
}

dependencies {
    compile project(':api')
    compile project(':common')
    compile project(':client')
    compile project(':server')
}

jar.finalizedBy('reobfJar')

jar {
    manifest {
        attributes([
                "Specification-Title"     : "kotlinmon",
                "Specification-Vendor"    : "landonjw",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "landonjw",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    dependencies {
        include project(':api')
        include project(':common')
        include project(':client')
        include project(':server')

        // Kotlin
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk7")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib")

        // Kotlin Reflect
        include dependency("org.jetbrains.kotlin:kotlin-reflect")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-common")
        include dependency("org.jetbrains:annotations")

        // Coroutines
        include dependency("org.jetbrains.kotlinx:kotlinx-coroutines-core")
        include dependency("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8")

        // Kodein
        include dependency("org.kodein.di:kodein-di-jvm")
        include dependency("org.kodein.type:kodein-type")
        include dependency("org.kodein.type:kodein-type-jvm")
    }
}

reobf {
    shadowJar {}
}

build.dependsOn(reobfShadowJar)